apply plugin: 'com.android.application'

static def buildTime() {
    return new Date().format("MM_dd_HH_mm", TimeZone.getDefault())
}

android {
    compileSdkVersion compile_version
    defaultConfig {
        applicationId "io.xdag.xdagwallet"
        minSdkVersion min_version
        targetSdkVersion target_version
        versionCode 10
        versionName "0.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [appName: "@string/app_name"]
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions"
            }
        }
        ndk {
            //abiFilters "armeabi-v7a","x86"
            abiFilters "armeabi-v7a"
        }
    }

    signingConfigs {
        release {
            keyAlias 'xdagwalltkey'
            keyPassword 'xdagtothemoon'
            storeFile file('xdagwallt.jks')
            storePassword 'xdagtothemoon'
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
            applicationIdSuffix '.debug'
            manifestPlaceholders = [appName: "@string/app_name_debug"]
        }
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "xdag_android_${variant.buildType.name}_v${defaultConfig.versionName}_${buildTime()}.apk"
        }
    }


    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':common')
    implementation project(':zbar')
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    // https://github.com/JakeWharton/butterknife
    implementation "com.jakewharton:butterknife:${butterknife_version}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknife_version}"
    // https://github.com/Tapadoo/Alerter
    implementation 'com.tapadoo.android:alerter:2.0.5'
    // https://github.com/yanzhenjie/AndPermission
    implementation 'com.yanzhenjie:permission:2.0.0-rc6'
    // https://github.com/aurelhubert/ahbottomnavigation
    implementation 'com.aurelhubert:ahbottomnavigation:2.1.0'
    // https://github.com/PureWriter/about-page
    implementation 'me.drakeet.support:about:2.1.1'
    implementation 'me.drakeet.multitype:multitype:3.4.4'
    // https://github.com/scottyab/rootbeer
    implementation('com.scottyab:rootbeer-lib:0.0.7', {
        exclude group: 'com.android.support'
    })
}
